{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Minimize walking distance for (non-connecting) departing passengers\n",
    "\n",
    "$$\n",
    "\\min \\sum_{f \\in F_{dep}, g \\in G} W_g P_f M_{f, g}\n",
    "$$\n",
    "\n",
    "Where: \n",
    "\n",
    "- $W_g$ is the walking distance from nearest security checkpoint to the gate\n",
    "- $P_{f}$ is the estimated number of passengers in departing flight $f$\n",
    "- $M_{f, g}$ is the binary decision variable. $M_{f, g} = 1$ if flight $f$ is assigned to gate $g$, $0$ otherwise\n",
    "\n",
    "Constraints:\n",
    "\n",
    "- No two aircrafts can occupy the same gate at the same time\n",
    "- If an aircraft is scheduled to depart within two hours of when it arrives, the in-bound and out-bound flights must be assigned the same gate\n",
    "\n",
    "\n",
    "Assumptions:\n",
    "- Gates are all shared among airlines.\n",
    "- Any aircraft can be parked at any gate.\n",
    "- Walking distances are from nearest security checkpoint to gate.\n",
    "- Only considering flights from terminals A, B, and C\n",
    "- Number of passengers are assumed based on the aircraft size: 75, 150, and 300 for regional, narrow-body, and wide-body aircraft, respectively.\n",
    "- If there's at least two hours between when an aircraft arrives at and departs from DFW, we assume it leaves it's arrival gate after 30 minutes for a hanger and enters its next gate 90 minutes before it's scheduled departure time\n",
    "\n",
    "### You can also minimize walking distance for (non-connecting) arriving passengers with the following objective function:\n",
    "\n",
    "$$\n",
    "\\min \\sum_{f \\in F_{arr}, g \\in G} W_b P_f M_{f, g}\n",
    "$$\n",
    "\n",
    "Where: \n",
    "\n",
    "- $W_b$ is the walking distance from gate to nearest baggage claim\n",
    "- $P_{f}$ is the estimated number of passengers in arriving flight $f$\n",
    "- $M_{f, g}$ is the binary decision variable. $M_{f, g} = 1$ if flight $f$ is assigned to gate $g$, $0$ otherwise"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Incorportating connecting passengers\n",
    "\n",
    "$$\n",
    "\\min \\sum_{f \\in F_{dep}, g \\in G} W_g P_{dep,f} M_{f, g} + \\sum_{f \\in F_{arr}, g \\in G} W_b P_{arr,f} M_{f, g} + \\sum_{f_1, f_2 \\in F} T_{f_1, f_2} \\cdot \\sum_{g_1, g_2 \\in G} W_{ g_1, g_2} \\cdot  M_{f_1, g_1} \\cdot M_{f_2, g_2}\n",
    "$$\n",
    "\n",
    "Where: \n",
    "\n",
    "$W_g$ is the walking distance from nearest security checkpoint to the gate \\\n",
    "$W_b$ is the walking distance from the gate to the nearest baggage claim \\\n",
    "$P_{dep,f}$ is the estimated number of non-connecting departing passengers in flight f \\\n",
    "$P_{arr,f}$ is the estimated number of non-connecting arriving passengers in flight f \\\n",
    "$W_{g_1, g_2}$ is the walking distance between gate $g_1$ and gate $g_2$ \\\n",
    "$T_{f_1, f_2}$ is the number of transfer passengers from flight $f_1$ to flight $f_2$ \\\n",
    "$M_{f, g}$ is the binary decision variable for gate assignments\n",
    "\n",
    "We linearize $M_{f_1, g_1} \\cdot M_{f_2, g_2}$ by introducing an auxillary variable $Z$:\n",
    "\n",
    "$$\n",
    "Z_{f_1, g_1, f_2, g_2} \\leq M_{f_1, g_1}, \\\\ Z_{f_1, g_1, f_2, g_2} \\leq M_{f_2, g_2} \\\\\n",
    "Z_{f_1, g_1, f_2, g_2} \\geq M_{f_1, g_1} + M_{f_2, g_2} - 1 \\\\\n",
    "Z{f_1, g_1, f_2, g_2} \\in \\{0,1\\}\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, Gurobi, CSV, DataFrames\n",
    "\n",
    "num_flights = 30\n",
    "BUFFER_TIME = 0\n",
    "\n",
    "function assign_gates(num_flights, departing = true, arriving = true, connecting = true, buffer_time = 0)\n",
    "    \"\"\"\n",
    "    This function assigns flights to gates based on the input parameters.\n",
    "    \n",
    "    Parameters:\n",
    "    - num_flights: number of flights to pull from dataset.\n",
    "    - departing: boolean variable. True if you want to minimize departing passengers' walking distance. False otherwise.\n",
    "    - arriving: boolean variable. True if you want to minimize arriving passengers' walking distance. False otherwise.\n",
    "    - connecting: boolean variable. True if you want to minimize connecting passengers' walking distance. False otherwise.\n",
    "    - buffer_time: enforced buffer time between gate occupancies in minutes. Default is 0.\n",
    "    \n",
    "    Saves\n",
    "    - A csv with optimal gate assignments\n",
    "\n",
    "    \"\"\"\n",
    "    # -------------------------------\n",
    "    # Load Data and Setup Parameters\n",
    "    # -------------------------------\n",
    "\n",
    "    df = CSV.read(\"Data/Final_Formatted_Sample_Day.csv\", DataFrame)\n",
    "    walking_distances = CSV.read(\"Data/Walking Distances Arriving and Departing Pax.csv\", DataFrame)\n",
    "    walking_distances_gate_to_gate = CSV.read(\"Data/Walking Distances Gate-to-Gate.csv\", DataFrame)\n",
    "    connections_matrix = CSV.read(\"Data/small_connections_matrix.csv\", DataFrame)\n",
    "\n",
    "    df = df[1:num_flights, :]\n",
    "    connections_matrix = connections_matrix[1:num_flights, 1:num_flights]\n",
    "    T_f1_f2 = zeros(num_flights,num_flights)\n",
    "    T_f1_f2[1:size(connections_matrix,1), 1:size(connections_matrix,2)-1] = Matrix(connections_matrix[1:size(connections_matrix,1), 2:end])\n",
    "\n",
    "    # Separate arriving and departing flights\n",
    "    departing_indices = findall(df.IsDeparting .== \"Y\")\n",
    "    arriving_indices  = findall(df.IsDeparting .== \"N\")\n",
    "    F_dep = length(departing_indices)   # Number of departing flights\n",
    "    F_arr = length(arriving_indices)    # Number of arriving flights\n",
    "    F     = num_flights                 # Total flights\n",
    "    G     = 96                          # Number of gates\n",
    "\n",
    "    # Define enter and exit gate times\n",
    "    df[!, :EnterGateTime] = df.ArrivalTimeMinutes\n",
    "    df[!, :ExitGateTime]  = df.OffTimeMinutes\n",
    "\n",
    "    # Passenger counts\n",
    "    P_df = [df.PassengersDept[departing_indices[f]] for f in 1:F_dep]  # Departing pax\n",
    "    P_af = [df.PassengersArr[arriving_indices[f]]  for f in 1:F_arr]   # Arriving pax\n",
    "\n",
    "    # Walking distances\n",
    "    W_g = walking_distances.TSA_to_Gate     # Gate distance from security\n",
    "    W_b = walking_distances.Gate_to_Bag     # Gate distance to baggage claim\n",
    "    W_g1_g2 = Matrix(walking_distances_gate_to_gate)\n",
    "\n",
    "    # -------------------------------\n",
    "    # Define the Model\n",
    "    # -------------------------------\n",
    "    model = Model(Gurobi.Optimizer)\n",
    "\n",
    "    # Gate‐assignment decision variables\n",
    "    @variable(model, M[1:F, 1:G], Bin)\n",
    "\n",
    "    connecting_pairs = Vector{Tuple{Int, Int}}()\n",
    "    if connecting   \n",
    "        # Pre-process connecting flight pairs\n",
    "        for f1 in 1:F\n",
    "            for f2 in 1:F\n",
    "                if T_f1_f2[f1, f2] > 0\n",
    "                    push!(connecting_pairs, (f1, f2))\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        # Auxiliary variables for linearizing M[f1,g1] * M[f2,g2]\n",
    "        @variable(model, Z[connecting_pairs, 1:G, 1:G], Bin)\n",
    "    end\n",
    "\n",
    "    # Minimize everybody's walking distance\n",
    "    @objective(model, Min,\n",
    "        departing * sum(W_g[g] * P_df[f] * M[departing_indices[f], g] for f in 1:F_dep, g in 1:G)\n",
    "        + arriving * sum(W_b[g] * P_af[f] * M[arriving_indices[f], g] for f in 1:F_arr, g in 1:G)\n",
    "        + connecting * sum(\n",
    "            T_f1_f2[f1, f2] * W_g1_g2[g1, g2] * Z[(f1,f2), g1, g2]\n",
    "            for (f1,f2) in connecting_pairs, g1 in 1:G, g2 in 1:G\n",
    "        )\n",
    "    )\n",
    "\n",
    "    if connecting\n",
    "        # Linearization constraints\n",
    "        @constraints(model, begin\n",
    "            [(f1,f2) in connecting_pairs, g1 in 1:G, g2 in 1:G], \n",
    "                Z[(f1,f2),g1,g2] <= M[f1,g1]\n",
    "            [(f1,f2) in connecting_pairs, g1 in 1:G, g2 in 1:G], \n",
    "                Z[(f1,f2),g1,g2] <= M[f2,g2]\n",
    "            [(f1,f2) in connecting_pairs, g1 in 1:G, g2 in 1:G], \n",
    "                Z[(f1,f2),g1,g2] >= M[f1,g1] + M[f2,g2] - 1\n",
    "        end)\n",
    "    end\n",
    "\n",
    "    # Each flight assigned to exactly one gate\n",
    "    @constraint(model, [f in 1:F], sum(M[f, g] for g in 1:G) == 1)\n",
    "    # -------------------------------\n",
    "    # Precompute Conflict Pairs\n",
    "    # -------------------------------\n",
    "    conflict_pairs = Vector{Tuple{Int, Int}}()\n",
    "    for f1 in 1:(F-1)\n",
    "        for f2 in (f1+1):F\n",
    "            if df.TailNumber[f1] != df.TailNumber[f2]\n",
    "                enter1  = df.EnterGateTime[f1]\n",
    "                depart1 = df.ExitGateTime[f1] + BUFFER_TIME\n",
    "                enter2  = df.EnterGateTime[f2]\n",
    "                depart2 = df.ExitGateTime[f2] + BUFFER_TIME\n",
    "                if (enter1 < depart2) && (enter2 < depart1)\n",
    "                    push!(conflict_pairs, (f1, f2))\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # No two conflicting flights may share the same gate\n",
    "    for (f1, f2) in conflict_pairs\n",
    "        for g in 1:G\n",
    "            @constraint(model, M[f1, g] + M[f2, g] <= 1)\n",
    "        end\n",
    "    end\n",
    "    # -------------------------------\n",
    "    # Precompute Same‐Gate Pairs\n",
    "    # -------------------------------\n",
    "    same_gate_pairs = Vector{Tuple{Int, Int}}()\n",
    "    for f1 in arriving_indices\n",
    "        for f2 in departing_indices\n",
    "            if df.TailNumber[f1] == df.TailNumber[f2] &&\n",
    "            (df.ExitGateTime[f1] + 120 >= df.EnterGateTime[f2])\n",
    "                push!(same_gate_pairs, (f1, f2))\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # If same tail number and close arrival/departure, force same gate\n",
    "    for (f1, f2) in same_gate_pairs\n",
    "        for g in 1:G\n",
    "            @constraint(model, M[f1, g] == M[f2, g])\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # -------------------------------\n",
    "    # Solve the Model\n",
    "    # -------------------------------\n",
    "    optimize!(model)\n",
    "\n",
    "    # Extract results\n",
    "    assignments = Dict(f => g for f in 1:F, g in 1:G if value(M[f, g]) ≈ 1)\n",
    "\n",
    "    # Create new columns for optimized gate assignments\n",
    "    df[!, :OptDepGate] = Vector{Union{String, Missing}}(missing, nrow(df))\n",
    "    df[!, :OptArrGate] = Vector{Union{String, Missing}}(missing, nrow(df))\n",
    "\n",
    "    # Gate mapping dictionary\n",
    "    gate_mapping = Dict(\n",
    "        1 => \"A8\", 2 => \"A9\", 3 => \"A10\", 4 => \"A11\", 5 => \"A13\", \n",
    "        6 => \"A14\", 7 => \"A15\", 8 => \"A16\", 9 => \"A17\", 10 => \"A18\", \n",
    "        11 => \"A19\", 12 => \"A20\", 13 => \"A21\", 14 => \"A22\", 15 => \"A23\", \n",
    "        16 => \"A24\", 17 => \"A25\", 18 => \"A28\", 19 => \"A29\", 20 => \"A33\", \n",
    "        21 => \"A34\", 22 => \"A35\", 23 => \"A36\", 24 => \"A37\", 25 => \"A38\", \n",
    "        26 => \"A39\", 27 => \"B1\", 28 => \"B2\", 29 => \"B3\", 30 => \"B4\", \n",
    "        31 => \"B5\", 32 => \"B6\", 33 => \"B7\", 34 => \"B9\", 35 => \"B10\", \n",
    "        36 => \"B11\", 37 => \"B12\", 38 => \"B14\", 39 => \"B16\", 40 => \"B17\", \n",
    "        41 => \"B18\", 42 => \"B19\", 43 => \"B21\", 44 => \"B22\", 45 => \"B24\", \n",
    "        46 => \"B25\", 47 => \"B26\", 48 => \"B27\", 49 => \"B28\", 50 => \"B29\", \n",
    "        51 => \"B30\", 52 => \"B31\", 53 => \"B32\", 54 => \"B33\", 55 => \"B34\", \n",
    "        56 => \"B35\", 57 => \"B36\", 58 => \"B37\", 59 => \"B38\", 60 => \"B39\", \n",
    "        61 => \"B40\", 62 => \"B42\", 63 => \"B43\", 64 => \"B44\", 65 => \"B46\", \n",
    "        66 => \"B47\", 67 => \"B48\", 68 => \"B49\", 69 => \"C2\", 70 => \"C4\", \n",
    "        71 => \"C6\", 72 => \"C7\", 73 => \"C8\", 74 => \"C10\", 75 => \"C11\", \n",
    "        76 => \"C12\", 77 => \"C14\", 78 => \"C15\", 79 => \"C16\", 80 => \"C17\", \n",
    "        81 => \"C19\", 82 => \"C20\", 83 => \"C21\", 84 => \"C22\", 85 => \"C24\", \n",
    "        86 => \"C26\", 87 => \"C27\", 88 => \"C28\", 89 => \"C29\", 90 => \"C30\", \n",
    "        91 => \"C31\", 92 => \"C33\", 93 => \"C35\", 94 => \"C36\", 95 => \"C37\", \n",
    "        96 => \"C39\"\n",
    "    )\n",
    "\n",
    "    # Assign gates\n",
    "    for f in 1:F\n",
    "        gate_number = get(assignments, f, missing)\n",
    "        if !ismissing(gate_number)\n",
    "            gate_code = get(gate_mapping, gate_number, missing)\n",
    "            if df.IsDeparting[f] == \"Y\"\n",
    "                df[f, :OptDepGate] = gate_code\n",
    "            else\n",
    "                df[f, :OptArrGate] = gate_code\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Save results\n",
    "    CSV.write(\"Optimized_Gate_Assignments_Sample_Day.csv\", df)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "assign_gates(31,true,true,false)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.5",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
